apply plugin: 'com.android.application'

def testProperties
def testPropertiesFile = file('../test.properties')
if (testPropertiesFile.canRead()) {
    testProperties = new Properties()
    testPropertiesFile.withInputStream {
        stream -> testProperties.load(stream)
    }
}

android {
    compileSdkVersion 16
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.vladsch.flexmark.android.test"
        minSdkVersion 16
        targetSdkVersion 16
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        maven
        snapshot
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        resultsDir = testProperties['path.report']
    }
}

repositories {
    flatDir {
        dirs '../../flexmark/target',
                '../../flexmark-ext-autolink/target',
                '../../flexmark-ext-gfm-strikethrough/target',
                '../../flexmark-ext-gfm-tables/target',
                '../../flexmark-test-util/target',
                '../../flexmark-ext-yaml-front-matter/target',
                '../../flexmark-test-util/target'
    }
}

dependencies {
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    androidTestCompile ':flexmark-test-util-' + testProperties['version.snapshot']

    androidTestMavenCompile 'org.nibor.autolink:autolink:' + testProperties['version.maven_autolink']
    androidTestMavenCompile 'com.vladsch.flexmark:flexmark:' + testProperties['version.maven']
    androidTestMavenCompile 'com.vladsch.flexmark:flexmark-ext-autolink:' + testProperties['version.maven']
    androidTestMavenCompile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:' + testProperties['version.maven']
    androidTestMavenCompile 'com.vladsch.flexmark:flexmark-ext-gfm-tables:' + testProperties['version.maven']
    androidTestMavenCompile 'com.vladsch.flexmark:flexmark-ext-yaml-front-matter:' + testProperties['version.maven']

    androidTestSnapshotCompile 'org.nibor.autolink:autolink:' + testProperties['version.snapshot_autolink']
    androidTestSnapshotCompile ':flexmark-' + testProperties['version.snapshot']
    androidTestSnapshotCompile ':flexmark-ext-autolink-' + testProperties['version.snapshot']
    androidTestSnapshotCompile ':flexmark-ext-gfm-strikethrough-' + testProperties['version.snapshot']
    androidTestSnapshotCompile ':flexmark-ext-gfm-tables-' + testProperties['version.snapshot']
    androidTestSnapshotCompile ':flexmark-ext-yaml-front-matter-' + testProperties['version.snapshot']
}
